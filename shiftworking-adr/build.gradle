apply plugin: 'com.android.application'

def APP_NAME = "ワクレコ work record"

android {
    compileSdkVersion 24
    buildToolsVersion '25.0.0'

    sourceSets {
        main {
            assets.srcDirs = ['assets']
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 2
        versionName "2.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    dataBinding {
        enabled = true
    }

    productFlavors {
        develop {
            applicationId "trente.asia.shiftworking"
            manifestPlaceholders = [appName:"${APP_NAME} DEV"]
            buildConfigField "Boolean", "DEVELOP", "true"
            buildConfigField "String", "HOST", "\"http://192.168.1.124:9000\""
        }
        production {
            applicationId "trente.fukuri.shiftworking"
            manifestPlaceholders = [appName:"${APP_NAME}"]
            buildConfigField "Boolean", "DEVELOP", "false"
            buildConfigField "String", "HOST", "\"https://tr-fukuri.jp\""
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name.equals("release")) {
            variant.outputs.each { output ->
                if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                    // Rename APK
                    def applicationId = defaultConfig.applicationId
                    def versionCode = defaultConfig.versionCode
                    def versionName = defaultConfig.versionName
                    def date = new java.text.SimpleDateFormat("yyyyMMddHHmm").format(new Date())
                    def newName = "${applicationId}_r${versionCode}v${versionName}_${date}.apk"
                    output.outputFile = new File(output.outputFile.parent, newName)
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(path: ':welfare-android-core')

//    compile 'com.google.maps.android:android-maps-utils:0.3.4'
    compile "com.daimajia.swipelayout:library:1.2.0@aar"
}

apply plugin: 'com.google.gms.google-services'

