apply plugin: 'com.android.application'
android {
    compileSdkVersion 24
    buildToolsVersion '25.0.0'

    sourceSets {
        main {
            assets.srcDirs = ['assets']
        }
    }

    defaultConfig {
        applicationId "trente.asia.messenger"
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 5
        versionName "1.4"
        multiDexEnabled true
    }

    dataBinding {
        enabled = true
    }

    productFlavors {
        develop {
            buildConfigField "Boolean", "DEVELOP", "true"
            buildConfigField "String", "HOST", "\"http://192.168.1.124:9000\""
        }
        production {
            buildConfigField "Boolean", "DEVELOP", "false"
            buildConfigField "String", "HOST", "\"https://tr-fukuri.jp\""
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name.equals("release")) {
            variant.outputs.each { output ->
                if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                    // Rename APK
                    def applicationId = defaultConfig.applicationId
                    def versionCode = defaultConfig.versionCode
                    def versionName = defaultConfig.versionName
                    def date = new java.text.SimpleDateFormat("yyyyMMddHHmm").format(new Date())
                    def newName = "${applicationId}_r${versionCode}v${versionName}_${date}.apk"
                    output.outputFile = new File(output.outputFile.parent, newName)
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':welfare-android-core')
    compile 'com.commit451:PhotoView:1.2.4'
    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    annotationProcessor 'com.bluelinelabs:logansquare-compiler:1.3.6'
}

apply plugin: 'com.google.gms.google-services'


