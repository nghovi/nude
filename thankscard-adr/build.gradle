buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
    }
}

apply plugin: 'com.android.application'

def APP_NAME = "Thanks card"

android {
    compileSdkVersion 24
    buildToolsVersion '25.0.0'

    sourceSets {
        main {
            assets.srcDirs = ['assets']
        }
    }

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        jackOptions {
            enabled true
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {
        develop {
            applicationId "trente.asia.thankscard"
            manifestPlaceholders = [appName:"${APP_NAME} DEV"]
            buildConfigField "Boolean", "DEVELOP", "true"
            buildConfigField "String", "HOST", "\"http://192.168.1.124:9000\""
        }
        production {
            applicationId "trente.fukuri.thankscard"
            manifestPlaceholders = [appName:"${APP_NAME}"]
            buildConfigField "String", "HOST", "\"https://tr-fukuri.jp\""
            buildConfigField "Boolean", "DEVELOP", "false"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name.equals("release")) {
            variant.outputs.each { output ->
                if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                    // Rename APK
                    def applicationId = defaultConfig.applicationId
                    def versionCode = defaultConfig.versionCode
                    def versionName = defaultConfig.versionName
                    def date = new java.text.SimpleDateFormat("yyyyMMddHHmm").format(new Date())
                    def newName = "${applicationId}_r${versionCode}v${versionName}_${date}.apk"
                    output.outputFile = new File(output.outputFile.parent, newName)
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':welfare-android-core')
    compile 'com.google.firebase:firebase-messaging:10.2.6'
}
apply plugin: 'com.google.gms.google-services'

